import java.nio.file.Files

plugins {
    // Plugin generates version info class.
    id 'com.peterabeles.gversion' version '1.10'
    id 'java-library'
}

dependencies {
    api asm
    api asm_analysis
    api asm_commons
    api asm_tree
    api asm_util
    api cafedude
    api jackson
    api slf4jLogging
    api ssvm // TODO: For making the maven release artifact, we want to shade SSVM and JLinker since they are not on Maven central
}

// Force generation of gversion data class when the version information is not up-to-date
tasks.register('conditionalBuildConfigUpdate') {
    if (!isBuildConfigUpToDate()) {
        finalizedBy createVersionFile
    }
}
project.compileJava.dependsOn('conditionalBuildConfigUpdate')
gversion {
    srcDir = "src/generated/java/"
    classPackage = "info.mmpa.concoction"
    className = "ConcoctionBuildConfig"
    dateFormat = "yyyy MM/dd HH:mm"
    debug = true
    language = "java"
    explicitType = false
    annotate = false
}
sourceSets {
    // Need to add the generated class to the source set
    main {
        java {
            srcDirs 'src/generated/java', 'src/main/java'
        }
    }
}

private boolean isBuildConfigUpToDate() {
    File buildConfigPath = project.file(gversion.srcDir +
            gversion.classPackage.replace('.', '/') + '/' +
            gversion.className + ".java")
    if (buildConfigPath.exists()) {
        String text = Files.readString(buildConfigPath.toPath())
        if (text.contains('VERSION = "' + project.version + '"'))
            return true
    }
    return false
}